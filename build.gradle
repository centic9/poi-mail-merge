apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

mainClassName = 'org.dstadler.poi.mailmerge.MailMerge'
version = '1.0'

jar {
    manifest {
        attributes 'Implementation-Title': 'Application to produce mail-merge document from a template and a list of values', 'Implementation-Version': archiveVersion
    }
}

sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    // maven { url 'https://repository.apache.org/content/groups/staging/'}
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.dstadler:commons-dost:1.1.0.0'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'org.apache.commons:commons-csv:1.9.0'

    implementation 'org.apache.poi:poi:5.1.0'
    implementation 'org.apache.poi:poi-ooxml:5.1.0'
    // ooxml-full is needed for now, a later version of POI may allow to use ooxml-lite instead
    implementation 'org.apache.poi:poi-ooxml-full:5.1.0'
    implementation 'org.apache.poi:poi-scratchpad:5.1.0'
    /*implementation files('/tmp/poi-bin-5.1.0/lib/commons-collections4-4.4.jar')
    implementation files('/tmp/poi-bin-5.1.0/lib/commons-math3-3.6.1.jar')
    implementation files('/tmp/poi-bin-5.1.0/ooxml-lib/commons-compress-1.21.jar')
    implementation files('/tmp/poi-bin-5.1.0/ooxml-lib/xmlbeans-5.0.2.jar')
    implementation files('/tmp/poi-bin-5.1.0/poi-5.1.0.jar')
    implementation files('/tmp/poi-bin-5.1.0/poi-ooxml-5.1.0.jar')
    //implementation files('/tmp/poi-bin-5.1.0/poi-ooxml-lite-5.1.0.jar')
    implementation files('/tmp/poi-bin-5.1.0/poi-ooxml-full-5.1.0.jar')
    implementation files('/tmp/poi-bin-5.1.0/poi-scratchpad-5.1.0.jar')*/

	implementation 'org.apache.logging.log4j:log4j-core:2.17.1'

    testImplementation 'junit:junit:4.13.2'
}

wrapper {
    gradleVersion = '6.6.1'
}

task adjustWrapperPropertiesFile {
    doLast {
        ant.replaceregexp(match: '^#.*', replace: '', flags: 'g', byline: true) {
            fileset(dir: project.projectDir, includes: 'gradle/wrapper/gradle-wrapper.properties')
        }
        new File(project.projectDir, 'gradle/wrapper/gradle-wrapper.properties').with {
            it.text = it.readLines().findAll { it }.sort().join('\n')
        }
        ant.fixcrlf(file: 'gradle/wrapper/gradle-wrapper.properties', eol: 'lf')
    }
}
wrapper.finalizedBy adjustWrapperPropertiesFile

test {
    systemProperties = System.properties

    // enable to show standard out and standard error of the test JVM(s) on the console
    // testLogging.showStandardStreams = true

    // http://forums.gradle.org/gradle/topics/jacoco_related_failure_in_multiproject_build
    systemProperties['user.dir'] = workingDir
}

jacoco {
    toolVersion = '0.8.7'
}
