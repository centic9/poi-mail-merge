buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.thetaphi:forbiddenapis:3.10'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    // https://github.com/researchgate/gradle-release
    id 'net.researchgate.release' version '3.1.0'
    id 'io.codearte.nexus-staging' version '0.30.0'

    // https://gradle-pitest-plugin.solidsoft.info/
    id 'info.solidsoft.pitest' version '1.9.11'
}

apply plugin: 'de.thetaphi.forbiddenapis'
apply plugin: 'jacoco'
apply plugin: 'signing'
apply plugin: 'application'

java {
    toolchain {
        sourceCompatibility = JavaVersion.VERSION_17
    }
}

group = 'org.dstadler'
archivesBaseName = 'poi-mail-merge'

application {
    mainClass = 'org.dstadler.poi.mailmerge.MailMerge'
    applicationDefaultJvmArgs = ['-Xmx256m', '-XX:+HeapDumpOnOutOfMemoryError', '-XX:HeapDumpPath=/tmp']
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Application to produce mail-merge document from a template and a list of values', 'Implementation-Version': archiveVersion
    }
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    // for testing newer Apache POI
    // maven { url 'https://repository.apache.org/content/groups/staging/' }
}

forbiddenApis {
    suppressAnnotations = ['org.dstadler.commons.util.SuppressForbidden']
    // 'jdk-unsafe'
    bundledSignatures = [ 'jdk-system-out', 'jdk-non-portable', 'jdk-deprecated', 'jdk-reflection',
                          'commons-io-unsafe-2.19.0', 'jdk-internal' ]
}
forbiddenApisMain {
    signaturesFiles += files('config/forbidden-apis/forbidden.signatures.txt')
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'org.dstadler:commons-dost:1.4.4'
    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'org.apache.commons:commons-csv:1.14.0'
    implementation 'org.apache.commons:commons-text:1.14.0'

    implementation 'org.apache.poi:poi:5.5.0'
    implementation 'org.apache.poi:poi-ooxml:5.5.0'
    implementation 'org.apache.poi:poi-ooxml-lite:5.5.0'
    implementation 'org.apache.poi:poi-scratchpad:5.5.0'

	implementation 'org.apache.logging.log4j:log4j-core:2.25.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:6.0.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.1'
}

wrapper {
    gradleVersion = '8.14.3'
}

test {
    systemProperties = System.properties

    // enable to show standard out and standard error of the test JVM(s) on the console
    // testLogging.showStandardStreams = true

    // http://forums.gradle.org/gradle/topics/jacoco_related_failure_in_multiproject_build
    systemProperties['user.dir'] = workingDir

    useJUnitPlatform()
}

jacoco {
    toolVersion = '0.8.14'
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}


java {
    withJavadocJar()
    withSourcesJar()
}

release {
    git {
        requireBranch.set('master')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            from components.java

            pom {
                name = 'POI Mail Merge'
                description = 'A small application which allows to repeatedly replace markers in a Microsoft Word ' +
                        'document with items taken from a CSV/Microsoft Excel file.'
                url = 'https://github.com/centic9/poi-mail-merge'
                licenses {
                    license {
                        name = 'BSD 2-Clause "Simplified" License'
                        url = 'https://github.com/centic9/poi-mail-merge/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'centic9'
                        name = 'Dominik Stadler'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:centic9/poi-mail-merge'
                    developerConnection = 'scm:git@github.com:centic9/poi-mail-merge'
                    url = 'https://github.com/centic9/poi-mail-merge'
                }
            }
        }
    }
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            //url = layout.buildDirectory.dir('repo')
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials(PasswordCredentials)
            //snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/')
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

afterReleaseBuild.dependsOn publish
